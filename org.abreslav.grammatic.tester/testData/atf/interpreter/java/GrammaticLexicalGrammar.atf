#import 
	'string.module'{string}, 
	'grammatic.typesystem'{some};

#import org.abreslav.grammatic.grammar.template.parser.SetComplementBuilder;
#import org.abreslav.grammatic.grammar.CharacterRange;
#import org.abreslav.grammatic.grammar.Expression;
#import org.abreslav.grammatic.template.TemplateBody;
#import org.abreslav.grammatic.parser.util.ListBuilder;

#javaoptions {
	grammarName = 'GrammaticLexicalGrammar';
	grammarPackage = 'org.abreslav.grammatic.grammar1';
}

basicLexicalAtom
	:  STRING
	:  character
	:  '.'
	:  range
	;
# --> (TemplateBody<? extends Expression> result) {
	STRING : result = createString(string.createString(STRING#));
	character : result = createCharacter(character#);
	'.' : result = createFullRange();
	range : result = range#;
}

range
	: '[' rangeItem+ ']'
 	: rangeComplement
	;
# --> (TemplateBody<? extends Expression> result) {
	ListBuilder<TemplateBody<? extends Expression>, org.abreslav.grammatic.grammar.template.grammarTemplate.AlternativeTemplate> listBuilder; 
	'['.before : listBuilder = createListBuilder();
	rangeItem:: item = rangeItem();
	rangeItem : addRangeItem(listBuilder, item);
	']'.after : result = getExpressionTemplate(listBuilder);
	rangeComplement : result = createComplementTemplate(rangeComplement#);
}

rangeComplement
	: '[^' rangeItem+ ']'
	;
# --> (SetComplementBuilder result) {
	'[^'.before : result = createComplementBuilder();
	rangeItem:: rangeComplementItem(result);
}
	
rangeItem
	: $only=character
	: $from=character '--' $to=character
	;
[[
	rangeComplementItem(SetComplementBuilder builder) {
		only : removeSubrange(builder, only#, only#);
		to : removeSubrange(builder, from#, to#);
	}
	rangeItem() --> (TemplateBody<CharacterRange> result) {
		only : result = createCharacterRange(only#, only#);
		to : result = createCharacterRange(from#, to#);
	}
]]
